// IN PROGRESS
// This is what I've worked out as a template for vertex shaders. Like you don't normally see shader info organized in this way, so I figured it's a good reference. I'll try to explain the point of things in comments

//The name and path. It's how Unity will identify this particular shader
Shader "Path/Name"
{
// All properties that can be passed into this shader
	Properties
	{
	// They're formatted like this:
	// _VariableName("Display Name", Type) = Default Value
		_RangedNumber("Ranged Number", Range(0.0, 3.0)) = 1.5
		_MainTex("Texture", 2D) = "white" {}
		_Color("Colour", Color) = (1,1,1,1)
		_Cube("Cube Map", Cube) = "" {}
	}
	//After properties, have the subshader
	SubShader
	{		
		//All tags are optional, often you'll have just RenderType and Queue
		Tags {
			// see here for more about RenderType https://docs.unity3d.com/Manual/SL-ShaderReplacement.html
			// Usually, you'll use just "Opaque" or "Transparent"
			"RenderType"="Opaque" 
			// Queue changes the default time when it's rendered. You can also set this in the material settings but this is default
			// possible values are Background, Transparent, Geometry, AlphaTest, Overlay and you can put a +1 or -1 to adjust from there
			"Queue" = "Transparent-1"
			// Other tags I don't use as much: 
			// "DisableBatching" I used this once when I had something that got broken when multiple meshes were batched together, and then I figured out a way to handle that.
			// "ForceNoShadowCasting"
			// "IgnoreProjector"
			// "CanUseSpriteAtlas"  Seems promising but I haven't used it
			// "PreviewType" 
		}
		
		//Blend says how it combines this with what was already rendered, if you're making it transparent
		// full list here: https://docs.unity3d.com/Manual/SL-Blend.html
		// Blend Off for opaque, but that's default
		// Blend SrcFactor DstFactor // where SrcFactor is what colour comes from this shader, and DstFactor is what colour was already on screen
		// So One One is add, 
		Blend One One
		
		//Zwrite and Ztest are about the ZBuffer, used for depth testing
		// Zwrite is On or Off, just to decide whether or not it writes its depth to the buffer
		ZWrite Off
		// ZTest has options of Less, Greater, LEqual, GEqual, Equal, NotEqual and Always
		// default is LEqual which means it renders this surface if it's distance from the camera is less or equal to what's already rendered
		ZTest LEqual
		// This offsets the ZWrite and ZTest, numbers are a slope multiplier and a distance offset, negative is closer, positive is further
		Offset 1, 1
		//Culling is for backface culling, default is Back but you can also use Front or Off
		Cull Off
		
		//you can specify certain shaders to be used for certain LODs
		LOD 100
		// A subshader can have multiple passes. Usually you only need one, but sometimes it's useful
		// I'm not getting into surface shaders right now, but if you were, you'd use that instead of everything inside Pass
		// Some of the stuff above, like Blend, ZWrite, Cull, you can also re-do those for each Pass and it will just do them all in sequence
		Pass
		{
		// this basically says use Cg/HLSL language for this pass
		// again, there are shaders that don't use this but I focused on just this language
			CGPROGRAM
			// We define the vertex and fragment functions we'll use
			// "vert" and "frag" are arbitrary names, and whatever we do there will need to be defined as a function further down
			#pragma vertex vert
			#pragma fragment frag
			// this imports a bunch of things that Unity provides. 
			// You can actually read it if you want to understand what some of the Unity, it'll be in your Unity installation
			//  under Editor/Data/CGIncludes  or you can see it online here: https://gist.github.com/hecomi/9580605
			#include "UnityCG.cginc"
			
			//you need to define two classes, 
			// one for what data is passed to the vertex function
			// the other for what data is passed from the vertex function to the fragment function
			struct appdata
			{
			//most properties are fixed, float, double which determines level of precision
			// maybe followed by number like 2 3 4 which makes it a vector of that number of elements
			// then this ALLCAPS declaration of what type of thing it is, which I don't know the full scope of what it can be
			// and sometimes it uses special words for variable names. Like, here viewDir does get the view direction, 
			//  not because it's TEXCOORD0 but because it's called viewDir? Or maybe because it's float3 ?
			// 
				float4 vertex : POSITION;
				float3 viewDir : TEXCOORD0;
				float3 normal : NORMAL;
				float4 color : COLOR;
				float2 uv : TEXCOORD1;
			};
			
			struct v2f
			{
				float4 vertex : SV_POSITION;
				float3 color : TEXCOORD0;
				float2 uv : TEXCOORD1;
			};
			
			//also define as variable any of the properties you want to access inside here
			float _RangedNumber;
			sampler2D _MainTex;
			float4 _MainTex_ST; // this gets the values from the Tiling and Offset
			fixed4 _Colour;
			uniform samplerCUBE _Cube;

			v2f vert (appdata v)
			{
				v2f o;
				//assign every property of o based on properties of v
				// this is called for each vertex in the mesh
				// We could fill a lot of space just on everything you can do in these functions, so I'm not doing it here
				return o;
			}
			
			fixed4 frag (v2f i) : SV_Target
			{
				// i will contain the results from the vertex function
				// they will be interpolated based on distance from the three vertices that make up this triangle
				// This function will calculate for each pixel what colour to display.
				fixed4 col = tex2D(_MainTex, i.uv);
				return col;
			}			

			//closes the CGPROGRAM
			ENDCG
		}
		
		//one way to reuse passes from multiple shaders so you're not just copying code over
		// described here: https://docs.unity3d.com/Manual/SL-UsePass.html requires you name your pass
		UsePass "Path/ShaderName/PassName"
	}
	// You can provide a fallback if for some reason this shader can't be used on this computer, it will use a different one
	// You provide the same Path/Name as the other shader has at the top
	FallBack "Path/OtherName"
}
