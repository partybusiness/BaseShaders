Shader "Unlit/#NAME#"
{
// This uses a cube map and displays it as though you saw it as a reflection.
// 
	Properties{
		_Cube("Reflection Map", Cube) = "" {}
	}
	SubShader{
		Pass{
			CGPROGRAM
			#pragma vertex vert
			#pragma fragment frag
			#include "UnityCG.cginc"
			uniform samplerCUBE _Cube;
			struct vertexInput {
				float4 vertex : POSITION;
				float3 normal : NORMAL;
			};
			struct vertexOutput {
				float4 pos : SV_POSITION;
				float3 normalDir : TEXCOORD0;
				float3 viewDir : TEXCOORD1;
			};
			vertexOutput vert(vertexInput input)
			{
				vertexOutput output;

				float4x4 modelMatrix = unity_ObjectToWorld;
				float4x4 modelMatrixInverse = unity_WorldToObject;
				// multiplication with unity_Scale.w is unnecessary
				// because we normalize transformed vectors
				output.viewDir = mul(modelMatrix, input.vertex).xyz
					- _WorldSpaceCameraPos;
				output.normalDir = normalize(
					mul(float4(input.normal, 0.0), modelMatrixInverse).xyz);
				output.pos = UnityObjectToClipPos(input.vertex);
				return output;
			}
			float4 frag(vertexOutput input) : COLOR
			{
			
			//if, instead of this, you just directly passed input.viewDir like so:
			//  return texCUBE(_Cube, input.viewDir);
			//  then the cubemap will look like a sky you can view through a window, defined by this mesh.
			//  And you could remove everything about normals
			
			// Also based on this you could access reflection probes:
			//  https://www.bitshiftprogrammer.com/2018/11/reflection-probe-custom-shader-tutorial.html
			//  float4 val = UNITY_SAMPLE_TEXCUBE(unity_SpecCube0, reflectedDir);
			//  reflectedColour.rgb = DecodeHDR(val, unity_SpecCube0_HDR);
			
				float3 reflectedDir = reflect(input.viewDir, normalize(input.normalDir));
				return texCUBE(_Cube, reflectedDir);
			}
			ENDCG
		}
	}
}

